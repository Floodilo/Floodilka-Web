//// SPDX-License-Identifier: AGPL-3.0-or-later
//// Copyright (C) 2020-2026 Fluxer Contributors
//// Copyright (C) 2026 Floodilka Contributors
//// Modified by Floodilka Contributors starting March 2026. See LICENSE and NOTICE.

import floodilka_admin/acl
import floodilka_admin/api/assets
import floodilka_admin/api/common
import floodilka_admin/components/flash
import floodilka_admin/components/layout
import floodilka_admin/components/ui
import floodilka_admin/constants
import floodilka_admin/web
import gleam/int
import gleam/list
import gleam/option
import gleam/string
import lustre/attribute as a
import lustre/element
import lustre/element/html as h
import wisp.{type Request, type Response}

pub fn view(
  ctx: web.Context,
  session: web.Session,
  current_admin: option.Option(common.UserLookupResult),
  flash_data: option.Option(flash.Flash),
  result: option.Option(assets.AssetPurgeResponse),
) -> Response {
  let has_permission = case current_admin {
    option.Some(admin) ->
      acl.has_permission(admin.acls, constants.acl_asset_purge)
    option.None -> False
  }

  let content =
    h.div([a.class("space-y-6")], [
      h.div([a.class("mb-6")], [ui.heading_page("Очистка ресурсов")]),
      h.div([a.class("text-sm text-neutral-600")], [
        element.text(
          "Удаление эмодзи или стикеров из хранилища и CDN. Укажите один или несколько ID (через запятую).",
        ),
      ]),
      case result {
        option.Some(response) -> render_result(response)
        option.None -> element.none()
      },
      case has_permission {
        True -> render_form()
        False -> render_permission_notice()
      },
    ])

  let html =
    layout.page(
      "Очистка ресурсов",
      "asset-purge",
      ctx,
      session,
      current_admin,
      flash_data,
      content,
    )
  wisp.html_response(element.to_document_string(html), 200)
}

fn render_form() {
  ui.card(ui.PaddingMedium, [
    ui.heading_card_with_margin("Очистить ресурсы"),
    h.p([a.class("text-sm text-neutral-500 mb-4")], [
      element.text(
        "Введите ID эмодзи или стикеров для удаления из S3 и кэша CDN.",
      ),
    ]),
    h.form(
      [
        a.method("POST"),
        a.action("?action=purge-assets"),
        a.class("space-y-4"),
      ],
      [
        h.div([a.class("space-y-2")], [
          h.label([a.class("text-sm font-medium text-neutral-700 mb-2 block")], [
            element.text("ID (через запятую или по одному на строку)"),
          ]),
          h.textarea(
            [
              a.name("asset_ids"),
              a.required(True),
              a.placeholder("123456789012345678\n876543210987654321"),
              a.attribute("rows", "4"),
              a.class(
                "w-full px-3 py-2 border border-neutral-300 rounded focus:outline-none focus:ring-2 focus:ring-neutral-900 text-sm",
              ),
            ],
            "",
          ),
        ]),
        h.div([a.class("space-y-2")], [
          h.label([a.class("text-sm font-medium text-neutral-700 mb-2 block")], [
            element.text("Причина для аудита (необязательно)"),
          ]),
          h.input([
            a.type_("text"),
            a.name("audit_log_reason"),
            a.placeholder("Запрос на удаление DMCA"),
            a.class(
              "w-full px-3 py-2 border border-neutral-300 rounded focus:outline-none focus:ring-2 focus:ring-neutral-900 text-sm",
            ),
          ]),
        ]),
        ui.button("Очистить ресурсы", "submit", ui.Danger, ui.Medium, ui.Full, []),
      ],
    ),
  ])
}

fn render_permission_notice() {
  ui.card(ui.PaddingMedium, [
    ui.heading_card_with_margin("Требуется разрешение"),
    h.p([a.class("text-sm text-neutral-600")], [
      element.text("Для использования этого инструмента нужен ACL asset:purge."),
    ]),
  ])
}

fn render_result(result: assets.AssetPurgeResponse) {
  h.div([a.class("space-y-4")], [
    ui.card(ui.PaddingMedium, [
      ui.heading_card_with_margin("Результат очистки"),
      h.div([a.class("text-sm text-neutral-600 mb-4")], [
        element.text(
          "Обработано "
          <> int.to_string(list.length(result.processed))
          <> " ID; "
          <> int.to_string(list.length(result.errors))
          <> " ошибок.",
        ),
      ]),
      render_processed_table(result.processed),
      case list.is_empty(result.errors) {
        True -> element.none()
        False -> render_errors(result.errors)
      },
    ]),
  ])
}

fn render_processed_table(items: List(assets.AssetPurgeResult)) {
  h.div([a.class("overflow-x-auto border border-neutral-200 rounded-lg")], [
    h.table([a.class("min-w-full text-left text-sm text-neutral-700")], [
      h.thead([a.class("bg-neutral-50 text-xs uppercase text-neutral-500")], [
        h.tr([], [
          h.th([a.class("px-4 py-2 font-medium")], [element.text("ID")]),
          h.th([a.class("px-4 py-2 font-medium")], [element.text("Тип")]),
          h.th([a.class("px-4 py-2 font-medium")], [element.text("В БД")]),
          h.th([a.class("px-4 py-2 font-medium")], [element.text("ID сервера")]),
        ]),
      ]),
      h.tbody([], {
        list.map(items, fn(item) {
          h.tr([a.class("border-t border-neutral-100")], [
            h.td([a.class("px-4 py-3 break-words")], [element.text(item.id)]),
            h.td([a.class("px-4 py-3")], [element.text(item.asset_type)]),
            h.td([a.class("px-4 py-3")], [
              element.text(case item.found_in_db {
                True -> "Да"
                False -> "Нет"
              }),
            ]),
            h.td([a.class("px-4 py-3")], [
              element.text(option.unwrap(item.guild_id, "—")),
            ]),
          ])
        })
      }),
    ]),
  ])
}

fn render_errors(errors: List(assets.AssetPurgeError)) {
  h.div([a.class("mt-4 space-y-2")], {
    list.map(errors, fn(err) {
      h.div([a.class("text-sm text-red-600")], [
        element.text(err.id <> ": " <> err.error),
      ])
    })
  })
}

pub fn handle_action(
  req: Request,
  ctx: web.Context,
  session: web.Session,
  current_admin: option.Option(common.UserLookupResult),
) -> Response {
  use form_data <- wisp.require_form(req)

  let ids_input =
    list.key_find(form_data.values, "asset_ids")
    |> option.from_result
    |> option.unwrap("")

  let normalized =
    string.replace(ids_input, "\n", ",")
    |> string.replace("\r", ",")

  let ids =
    string.split(normalized, ",")
    |> list.map(string.trim)
    |> list.filter(fn(id) { !string.is_empty(id) })

  let audit_log_reason =
    list.key_find(form_data.values, "audit_log_reason")
    |> option.from_result

  case list.is_empty(ids) {
    True ->
      flash.redirect_with_error(ctx, "/asset-purge", "Укажите хотя бы один ID.")
    False ->
      case assets.purge_assets(ctx, session, ids, audit_log_reason) {
        Ok(response) ->
          view(ctx, session, current_admin, option.None, option.Some(response))
        Error(_) ->
          flash.redirect_with_error(
            ctx,
            "/asset-purge",
            "Не удалось очистить ресурсы.",
          )
      }
  }
}
